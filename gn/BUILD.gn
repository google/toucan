# Copyright 2023 The Toucan Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

config("default") {
  if (is_win) {
    include_dirs = [
      "$win_vc/Tools/MSVC/$win_toolchain_version/include",
      "$win_sdk/Include/$win_sdk_version/shared",
      "$win_sdk/Include/$win_sdk_version/ucrt",
      "$win_sdk/Include/$win_sdk_version/um",
      "$win_sdk/Include/$win_sdk_version/winrt",
    ]
    lib_dirs = [
      "$win_sdk/Lib/$win_sdk_version/ucrt/$current_cpu",
      "$win_sdk/Lib/$win_sdk_version/um/$current_cpu",
      "$win_vc/Tools/MSVC/$win_toolchain_version/lib/$current_cpu",
    ]
    if (is_debug) {
      cflags = [ "/MDd" ]
    } else {
      cflags = [ "/MD" ]
    }
    cflags_cc = [ "/std:c++20" ]
    defines = [
      "_HAS_EXCEPTIONS=0",
      "NOMINMAX",
    ]
    ldflags = [ "/DEBUG:FASTLINK" ]
  } else {
    cflags_cc = [ "-std=c++20" ]
    cflags_objcc = [ "-std=c++20" ]
    defines = []
    if (is_linux) {
      cflags = [ "-fPIC" ]
    }
    if (current_os == "android") {
      cflags = [ "-fPIC", "--target=$ndk_target$ndk_api" ]
      ldflags = [ "--target=$ndk_target$ndk_api" ]
    } else if (current_os == "ios") {
      if (current_cpu != host_cpu) {
        _arch_flags = [ "-arch", current_cpu, ]
      } else {
        _arch_flags = []
      }
      xcode_ios_sysroot = exec_script("../tools/run.py", [ "xcrun", "--sdk", "iphoneos", "--show-sdk-path" ], "trim string")
      cflags = [ "-isysroot", xcode_ios_sysroot ] + _arch_flags
      ldflags = [ "-isysroot", xcode_ios_sysroot ] + _arch_flags
    }
  }
  if (!is_debug) {
    defines += [ "NDEBUG" ]
  }

  defines += [ "STACK_SIZE=" + stack_size ]

  if (target_os == "android") {
    defines += [ "TARGET_OS_IS_ANDROID=1" ]
  } else {
    defines += [ "TARGET_OS_IS_ANDROID=0" ]
  }
  if (target_os == "linux") {
    defines += [ "TARGET_OS_IS_LINUX=1" ]
  } else {
    defines += [ "TARGET_OS_IS_LINUX=0" ]
  }
  if (target_os == "mac") {
    defines += [ "TARGET_OS_IS_MAC=1" ]
  } else {
    defines += [ "TARGET_OS_IS_MAC=0" ]
  }
  if (target_os == "ios") {
    defines += [ "TARGET_OS_IS_IOS=1" ]
  } else {
    defines += [ "TARGET_OS_IS_IOS=0" ]
  }
  if (target_os == "win") {
    defines += [ "TARGET_OS_IS_WIN=1" ]
  } else {
    defines += [ "TARGET_OS_IS_WIN=0" ]
  }
  if (target_os == "wasm") {
    defines += [ "TARGET_OS_IS_WASM=1" ]
  } else {
    defines += [ "TARGET_OS_IS_WASM=0" ]
  }

  if (target_cpu == "x86") {
    defines += [ "TARGET_CPU_IS_X86=1" ]
  } else {
    defines += [ "TARGET_CPU_IS_X86=0" ]
  }
  if (target_cpu == "x64") {
    defines += [ "TARGET_CPU_IS_X64=1" ]
  } else {
    defines += [ "TARGET_CPU_IS_X64=0" ]
  }
  if (target_cpu == "arm") {
    defines += [ "TARGET_CPU_IS_ARM32=1" ]
  } else {
    defines += [ "TARGET_CPU_IS_ARM32=0" ]
  }
  if (target_cpu == "arm64") {
    defines += [ "TARGET_CPU_IS_ARM64=1" ]
  } else {
    defines += [ "TARGET_CPU_IS_ARM64=0" ]
  }
  if (target_cpu == "wasm") {
    defines += [ "TARGET_CPU_IS_WASM=1" ]
  } else {
    defines += [ "TARGET_CPU_IS_WASM=0" ]
  }
}

config("debug_symbols") {
  if (is_win) {
    cflags = [ "/Z7"]
    ldflags = [ "/DEBUG:FASTLINK" ]
  } else {
    cflags = [ "-g" ]
  }
}

config("disable-flex-warnings") {
  if (!is_win) {
    cflags = [ "-Wno-deprecated-register" ]
  }
}
